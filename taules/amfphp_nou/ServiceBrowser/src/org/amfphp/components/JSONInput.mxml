<?xml version="1.0" encoding="utf-8"?>

<!--
        This file is part of AMFPHP
    
        It is released under the GPL License:
    
        This program is free software; you can redistribute it and/or
        modify it under the terms of the GNU General Public License (GPL)
        as published by the Free Software Foundation; either version 2
        of the License, or (at your option) any later version.
    
        This program is distributed in the hope that it will be useful,
        but WITHOUT ANY WARRANTY; without even the implied warranty of
        MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
        GNU General Public License for more details.
        
        To read the license please visit http://www.gnu.org/copyleft/gpl.html
-->

<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" verticalAlign="middle" xmlns:adobe="http://www.adobe.com/2006/fc"
		xmlns:controls="com.adobe.flex.extras.controls.*" creationComplete="text = null">
	<controls:AutoComplete text="{text}" width="100%" styleName="code" dataProvider="{autocomplete}" keyUp="bindText(event)"/>
	<mx:Image source="{valid ? correctIcon : incorrectIcon}"
			toolTip="The string you have entered is in {valid ? 'valid' : 'invalid'} JSON form{jsonError}"/>
	
	<mx:Metadata>
		<![CDATA[
			/**
			 * This class allows the user to enter any value, receive "autocomplete" prompts for common or previously used values and 
			 * JSON validation
			 * 
			 * @see http://www.adobe.com/cfusion/exchange/index.cfm?event=extensionDetail&extid=1047291
			 * 
			 * @author Danny Kopping danny@ria-coder.com
			 */
		]]>
	</mx:Metadata>
	
	<mx:Script>
		<![CDATA[
			import json.JParser;
			import mx.collections.ArrayCollection;
			
			[Bindable] [Embed(source="../../../assets/icons/accept.png")] private var correctIcon:Class;
			[Bindable] [Embed(source="../../../assets/icons/delete.png")] private var incorrectIcon:Class;
			
			[Bindable]
			/**
			 * The collection of autocompletion options - this value gets updated every time a method is run so that previously used 
			 * input values can be stored in the array
			 */
			public var autocomplete:Array;
			
			[Bindable]
			/**
			 * The value of the input
			 */
			public var text:String;
			
			[Bindable]
			/**
			 * Flag to indicate the JSON validity of the input
			 */
			public var valid:Boolean;
			
			[Bindable]
			/**
			 * An error string describing the JSON parsing error if one occured
			 */
			public var jsonError:String = "";
			
			/**
			 * Used to bind the value of the text property to the value of the AutoComplete field
			 * and to validate the JSON upon input
			 */
			protected function bindText(event:Event):void
			{
				if(event.currentTarget is AutoComplete)
				{
					text = event.currentTarget.text;
					valid = validateJSON(text);
				}
			}
			
			/**
			 * Validates the input of this field by attempting to decode the input as JSON.
			 * 
			 * @param input The value to validate
			 * @return <code>true</code> if the text is valid JSON, <code>false</code> otherwise
			 */
			protected function validateJSON(input:String):Boolean
			{
				var valid:Boolean = false;
				try
				{
					JParser.decode(input);
				}
				catch(e:Error)
				{
					jsonError = ".\n\nJSON Error\n" + e.message;
					valid = false;
					return valid;
				}
				
				jsonError = "";
				valid = true;
				return valid;
			}
		]]>
	</mx:Script>
</mx:HBox>